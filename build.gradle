/*
 * Copyright (c) 2021 Stephen Saville
 *
 * Licensed under the Apache License, Version 2.0 (the "License");
 * you may not use this file except in compliance with the License.
 * You may obtain a copy of the License at
 *
 *     http://www.apache.org/licenses/LICENSE-2.0
 *
 * Unless required by applicable law or agreed to in writing, software
 * distributed under the License is distributed on an "AS IS" BASIS,
 * WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
 * See the License for the specific language governing permissions and
 * limitations under the License.
 */

plugins {
    id "application"
    id "org.openjfx.javafxplugin" version "0.0.9"
    id "io.freefair.lombok" version "5.3.0"
}

group "com.gcodebuilder"
version "2021.01"

sourceSets {
    main {
        java {
            srcDirs = ["src"]
        }
    }
    test {
        java {
            srcDirs = ["test"]
        }
    }
}

processResources {
    from "src"
    include "**/*.png"
    include "**/*.fxml"
    include "log4j2.xml"
}

distributions {
    main {
        contents {
            from "README.md"
            from "LICENSE.txt"
            from "LICENSE-notice.md"
        }
    }
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(11)
    }

    modularity.inferModulePath = true
}

javafx {
    version = "15.0.1"
    modules = [ "javafx.controls", "javafx.fxml", "javafx.swing" ]
}

repositories {
    mavenCentral()
}

dependencies {
    implementation "com.google.guava:guava:30.1-jre"
    implementation "com.fasterxml.jackson.core:jackson-databind:2.12.1"
    implementation "org.apache.logging.log4j:log4j-api:2.14.0"
    implementation "org.apache.logging.log4j:log4j-core:2.14.0"

    testImplementation "org.junit.jupiter:junit-jupiter:5.7.0"
}

application {
    mainClass = "com.gcodebuilder.app.Main"
}

startScripts {
    doLast {
        unixScript.text = unixScript.text.replace('DEFAULT_JVM_OPTS=""', 'DEFAULT_JVM_OPTS="--module-path $APP_HOME/lib --add-modules javafx.controls,javafx.fxml,javafx.graphics"')
        windowsScript.text = windowsScript.text.replace('DEFAULT_JVM_OPTS=', 'DEFAULT_JVM_OPTS="--module-path" "%APP_HOME%/lib" "--add-modules" "javafx.controls,javafx.xml,javafx.graphics"')
    }
}
